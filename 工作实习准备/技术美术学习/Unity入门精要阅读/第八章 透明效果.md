### 第八章 透明效果

- 实现透明效果,通常会在渲染模型时控制它的**透明通道（Alpha Channel）**

当**开启透明混合**后，当一个物体被渲染到屏幕上时，每个片元除了颜色值和深度值之外，它还有另一个属性——**透明度**。

- 透明度为1:完全不透明
- 透明度为0:该像素完全不会显示



在Unity中，我们通常使用两种方法来实现透明效果：第一种是使用**透明度测试（Alpha Test）** ，这种方法其实无法得到真正的半透明效果；另一种是**透明度混合（Alpha Blending）** 。

- 对于不透明（opaque）物体，**不考虑它们的渲染顺序也能得到正确的排序效果**，这是由于强大的深度缓冲（depth buffer，也被称为z-buffer）的存在。

  - 深度缓冲是用于**解决可见性（visibility）问题的**,当渲染一个片元时，需要把它的深度值和已经存在于深度缓冲中的值进行比较（如果开启了**深度测试**），如果它的值距离摄像机更远，那么说明这个片元不应该被渲染到屏幕上（有物体挡住了它）；否则，这个片元应该覆盖掉此时颜色缓冲中的像素值，并把它的深度值更新到深度缓冲中（如果开启了**深度写入**）。
  - (**是否开启深度测试决定是否会对深度进行比较,而是否开启深度写入会决定新的深度值是否应该更新到深度缓冲区中**)

  - 使用深度缓冲，可以让我们不用关心不透明物体的渲染顺序.

- 对于透明物体,事情就不那么简单了,这是因为，**当使用透明度混合时，我们关闭了深度写入（ZWrite）。**



简单来说，透明度测试和透明度混合的基本原理如下:

- **透明度测试** ：它采用一种“霸道极端”的机制，**只要一个片元的透明度不满足条件（通常是小于某个阈值），那么它对应的片元就会被舍弃。被舍弃的片元将不会再进行任何处理，也不会对颜色缓冲产生任何影响；**否则，就会按照普通的不透明物体的处理方式来处理它，即进行深度测试、深度写入等。也就是说，透明度测试是不需要关闭深度写入的，它和其他不透明物体最大的不同就是它会根据透明度来舍弃一些片元。虽然简单，但是它产生的效果也很极端，要么完全透明，即看不到，要么完全不透明，就像不透明物体那样。
- **透明度混合** ：这种方法可以得到真正的半透明效果。**它会使用当前片元的透明度作为混合因子，与已经存储在颜色缓冲中的颜色值进行混合，得到新的颜色。**但是，**透明度混合需要关闭深度写入**（我们下面会讲为什么需要关闭），这使得我们要非常小心物体的渲染顺序。需要注意的是，**透明度混合只关闭了深度写入，但没有关闭深度测试。**这意味着，当使用透明度混合渲染一个片元时，还是会比较它的深度值与当前深度缓冲中的深度值，**如果它的深度值距离摄像机更远，那么就不会再进行混合操作。**这一点决定了，当一个不透明物体出现在一个透明物体的前面，而我们先渲染了不透明物体，它仍然可以正常地遮挡住透明物体。也就是说，**对于透明度混合来说，深度缓冲是只读的。**

[(1 封私信) 为什么半透明模型的渲染要使用深度测试而关闭深度写入？ - 知乎 (zhihu.com)](https://www.zhihu.com/question/60898307)

#### 8.1 为什么渲染顺序很重要



> 进行半透明渲染需要开启深度测试的原因是，如果半透明物体被不透明物体挡住了，那么该fragments是不需要写入到帧缓冲的，也就是说开启深度测试是为了保证不透明物体和半透明物体之间正常的遮挡关系；而关闭深度写入也很好理解，因为开启深度写入后我们会得到这样一种错误的结果，那就是近处的半透明fragments居然会挡住远处的半透明fragments，这和我们前面说到的我们可以透过半透明物体看到物体后面的东西是矛盾的！



![image-20220223112050849](E:\各种总结指南\技术美术学习\技术美术相关\Unity Shader入门精要阅读\第八章 透明效果.assets\image-20220223112050849.png)



- 不透明物体应该在半透明物体之前被渲染;
- 对于半透明物体之间,渲染顺序也非常重要,见下:
- ![image-20220223112354864](E:\各种总结指南\技术美术学习\技术美术相关\Unity Shader入门精要阅读\第八章 透明效果.assets\image-20220223112354864.png)



##### **结论(在开启深度测试但是关闭深度写入之后):**

- 先渲染所有的不透明物体,开启深度测试和深度写入;
- 把半透明物体按它们距离摄像机的远近进行排序，然后按照从后往前的顺序渲染这些半透明物体，并开启它们的深度测试，但关闭深度写入。
  - 值得注意的是,**这是物体级别的排序,所以下述情况难以处理:**
  - <img src="E:\各种总结指南\技术美术学习\技术美术相关\Unity Shader入门精要阅读\第八章 透明效果.assets\image-20220223112610934.png" alt="image-20220223112610934" style="zoom:80%;" />

<img src="E:\各种总结指南\技术美术学习\技术美术相关\Unity Shader入门精要阅读\第八章 透明效果.assets\image-20220223112721401.png" alt="image-20220223112721401" style="zoom:67%;" />

此时的问题是,如果我们选定物体上的一个特定点(比如中点)作为深度比较的依据,那么A会在B的前面(在上图中,不管我们选择物体上的哪个顶点都是这个结果,比如头部的点,中间的点,后面的点),但实际上A有一部分会被B遮挡.

- **这种问题的解决方法通常也是分割网格。**



尽管结论是，总是会有一些情况打乱我们的阵脚，**但由于上述方法足够有效并且容易实现，因此大多数游戏引擎都使用了这样的方法**为了减少错误排序的情况，**我们可以尽可能让模型是凸面体**，并且**考虑将复杂的模型拆分成可以独立排序的多个子模型等**。其实就算排序错误结果有时也不会非常糟糕，如果我们不想分割网格，可以试着让透明通道更加柔和，使穿插看起来并不是那么明显。**我们也可以使用开启了深度写入的半透明效果来近似模拟物体的半透明（详见8.5节）。**





#### 8.2 Unity Shader的渲染顺序

Unity为了解决渲染顺序的问题提供了**渲染队列** **（render queue）** 这一解决方案。我们可以使用`SubShader`的**Queue** 标签来决定我们的模型将归于哪个渲染队列。**Unity在内部使用一系列整数索引来表示每个渲染队列，且索引号越小表示越早被渲染。**在Unity 5中，Unity提前定义了5个渲染队列，**当然在每个队列中间我们可以使用其他队列**。表8.1给出了这5个提前定义的渲染队列以及它们的描述。

![image-20220223113218803](E:\各种总结指南\技术美术学习\技术美术相关\Unity Shader入门精要阅读\第八章 透明效果.assets\image-20220223113218803.png)

因此，如果我们想要通过透明度测试实现透明效果，代码中应该包含类似下面的代码：

```c#
SubShader {
    Tags { "Queue"="AlphaTest" }  //使用透明度测试
    Pass {
        ...
    }
}
```

如果我们想要通过**透明度混合**来实现透明效果，代码中应该包含类似下面的代码:

```c#
SubShader {
    Tags { "Queue"="Transparent" }
    Pass {
        ZWrite Off   //关闭透明度写入
        ...
    }
}
```

其中，**ZWrite Off** 用于关闭深度写入，在这里我们选择把它写在Pass中。我们也可以把它写在SubShader中，这意味着该SubShader下的所有Pass都会关闭深度写入。



#### 8.3 透明度测试

- 在这种情况下,透明度测试得到的透明效果很“极端”——要么完全透明，要么完全不透明，它的效果往往像在一个不透明物体上挖了一个空洞。而且，得到的透明效果在边缘处往往参差不齐，有锯齿，这是因为在边界处纹理的透明度的变化精度问题。为了得到更加柔滑的透明效果，就可以使用透明度混合。



我们来看一下如何在Unity中实现透明度测试的效果。在上面我们已经知道了透明度测试的工作原理。

**透明度测试：** 只要一个片元的透明度不满足条件（通常是小于某个阈值），那么它对应的片元就会被舍弃。被舍弃的片元将不会再进行任何处理，也不会对颜色缓冲产生任何影响；否则，就会按照普通的不透明物体的处理方式来处理它。

通常，我们会在片元着色器中使用clip函数来进行透明度测试。clip是Cg中的一个函数，它的定义如下。

**函数** ：`void clip(float4 x); `

`void clip(float3 x); `

`void clip(float2 x);`

` void clip(float1 x);` 

` void clip(float x);`

**参数** ：裁剪时使用的标量或矢量条件。

**描述** ：如果给定参数的任何一个分量是负数，就会舍弃当前像素的输出颜色。它等同于下面的代码：

```c#
void clip(float4 x)
{
    if (any(x &lt; 0))  //Test if any component of x is nonzero.any(x) 参数中只要有一个不为0就返回true 否则返回false,这里没太看懂,但大概逻辑就如上面所说,如果给定参数的任何一个分量是负数，就会舍弃当前像素的输出颜色。
        discard;
}
```

在本节中，我们使用图8.5中的半透明纹理来实现透明度测试。在本书资源中，该纹理名为transparent_texture.psd。该透明纹理在不同区域的透明度也不同，我们通过它来查看透明度测试的效果。



#### 本章目标1:

![image-20220223114322429](E:\各种总结指南\技术美术学习\技术美术相关\Unity Shader入门精要阅读\第八章 透明效果.assets\image-20220223114322429.png)

1.新建shader,material等资源;

2.为了在材质面板中控制透明度测试时使用的阈值，我们在Properties语义块中声明一个范围在[0, 1]之间的属性_Cutoff：

```c#
	Properties
	{
		_Color("Color Tint",Color)=(1,1,1,1)
		_MainTex("Main Tex",2D) = "white"{}
		_CutOff("Alpha Cutoff",Range(0,1))=0.5 //_Cutoff参数用于决定我们调用clip进行透明度测试时使用的判断条件。
			//它的范围是[0，1]，这是因为纹理像素的透明度就是在此范围内。
	}
```

然后，我们在SubShader语义块中定义了一个Pass语义块：

```c#
    SubShader
	{
		Tags { "Queue"="AlphaTest" "IgnoreProjector" = "True" "RenderType" = "TransparentCutout" }
		Pass
		{
			Tags {"LightMode"="ForwardBase"}
			CGPROGRAM
			#pragma vertex vert
			#pragma fragment frag
			#include "Lighting.cginc"

			ENDCG
		}
	}
```

我们在8.2节中已经知道渲染顺序的重要性，并且知道在**Unity中透明度测试使用的渲染队列是名为AlphaTest的队列**，因此我们需要把**Queue标签设置为AlphaTest**。

而RenderType标签可以让Unity把这个Shader归入到提前定义的组（这里就是TransparentCutout组）中，以指明该Shader是一个使用了透明度测试的Shader。

- RenderType标签通常被用于着色器替换功能。

我们还把IgnoreProjector设置为True，这意味着这个Shader不会受到投影器（Projectors）的影响。

**通常，使用了透明度测试的Shader都应该在SubShader中设置这三个标签。**最后，LightMode标签是Pass标签中的一种，它用于定义该Pass在Unity的光照流水线中的角色。只有定义了正确的LightMode，我们才能正确得到一些Unity的内置光照变量，例如_LightColor0。



3.为了和Properties语义块中声明的属性建立联系，我们需要定义和各个属性类型相匹配的变量：

```c#
fixed4 _Color;
sampler2D _MainTex;
float4 _MainTex_ST; //对于纹理项来说,要注意这两个的类型,sampler2D以及float4,后面还要加_ST
fixed _CutOff;
```

由于_Cutoff的范围在[0, 1]，因此我们可以使用fixed精度来存储它。



4.然后，我们定义了顶点着色器的输入和输出结构体，接着定义顶点着色器：

```c#
		struct a2v
				{
					float4 vertex:POSITION;
					float3 normal:NORMAL;
					float4 texcoord:TEXCOORD0;
				};
				struct v2f
				{
					float4 pos:SV_POSITION;
					float3 worldNormal:TEXCOORD0;
					float3 worldPos:TEXCOORD1; //worldNormal和worldPos是用于后续在片元着色器当中计算光照的diffuse项的
					float2 uv:TEXCOORD2;
				};

				v2f vert(a2v v)
				{
					v2f o;
					o.pos = UnityObjectToClipPos(v.vertex); //输入顶点坐标通过MVP矩阵变换到裁剪空间
					o.worldNormal= UnityObjectToWorldNormal(v.normal); //世界坐标法线转换
					o.worldPos = mul(unity_ObjectToWorld, v.vertex).xyz;  //顶点坐标世界空间
					o.uv = TRANSFORM_TEX(v.texcoord, _MainTex); //通过*tiling+offset将纹理图转换为uv
					return o;
				}
```

补充内容:

[(7条消息) Unity_Shader 之TRANSFORM_TEX详解_lalate的专栏-CSDN博客_shader transform_tex](https://blog.csdn.net/lalate/article/details/91479715)

上面的代码我们已经见到过很多次了，我们在顶点着色器计算出世界空间的法线方向和顶点位置以及变换后的纹理坐标，再把它们传递给片元着色器。

5.**最重要的片元着色器**

```c#
				fixed4 frag(v2f i):SV_Target
				{
					fixed3 worldNormal = normalize(i.worldNormal);
					fixed3 worldLightDir = normalize(UnityWorldSpaceLightDir(i.worldPos)); // 光照方向,这两项可以用来计算漫反射项
					fixed4 texColor = tex2D(_MainTex, i.uv); //对纹理进行采样

					//进行透明度测试,不符合要求的直接舍弃掉
					clip(texColor.a - _CutOff);
					fixed3 albedo = texColor.rgb*_Color.rgb;
					fixed3 diffuse = _LightColor0.rgb*albedo*max(dot(worldNormal, worldLightDir), 0);
					fixed3 ambient = UNITY_LIGHTMODEL_AMBIENT.xyz * albedo;

					return fixed4(diffuse+ambient, 1.0);
				}
```

前面我们已经提到过clip函数的定义，它会判断它的参数，即texColor.a - _ Cutoff是否为负数，如果是就会舍弃该片元的输出。也就是说，当texColor.a小于材质参数_Cutoff时，该片元就会产生完全透明的效果。使用clip函数等同于先判断参数是否小于零，如果是就使用discard指令来显式剔除该片元。后面的代码和之前使用过的完全一样，我们计算得到环境光照和漫反射光照，把它们相加后再进行输出。



6.为Shader设置合适的Fallback

```c#
Fallback "Transparent/Cutout/VertexLit"
```

和之前使用的Diffuse和Specular不同，这次我们使用内置的Transparent/Cutout/VertexLit来作为回调Shader。这不仅能够保证在我们编写的SubShader无法在当前显卡上工作时可以有合适的代替Shader，还可以保证使用透明度测试的物体可以正确地向其他物体投射阴影，具体原理可以参见9.4.5节。



总的代码:

```c#
// Upgrade NOTE: replaced '_Object2World' with 'unity_ObjectToWorld'
// Upgrade NOTE: replaced 'mul(UNITY_MATRIX_MVP,*)' with 'UnityObjectToClipPos(*)'

Shader "MyShader/alpha/alpha1test"
{
	Properties
	{
		_Color("Color Tint",Color)=(1,1,1,1)
		_MainTex("Main Tex",2D) = "white"{}
		_CutOff("Alpha Cutoff",Range(0,1)) = 0.5 //_Cutoff参数用于决定我们调用clip进行透明度测试时使用的判断条件。
			//它的范围是[0，1]，这是因为纹理像素的透明度就是在此范围内。
	}
		SubShader
		{
			Tags { "Queue" = "AlphaTest" "IgnoreProjector" = "True" "RenderType" = "TransparentCutout" }
			Pass
			{
				Tags {"LightMode" = "ForwardBase"}
				CGPROGRAM
				#pragma vertex vert
				#pragma fragment frag
				#include "Lighting.cginc"
				fixed4 _Color;
				sampler2D _MainTex;
				float4 _MainTex_ST;
				fixed _CutOff;

				struct a2v
				{
					float4 vertex:POSITION;
					float3 normal:NORMAL;
					float4 texcoord:TEXCOORD0;
				};
				struct v2f
				{
					float4 pos:SV_POSITION;
					float3 worldNormal:TEXCOORD0;
					float3 worldPos:TEXCOORD1;
					float2 uv:TEXCOORD2;
				};

				v2f vert(a2v v)
				{
					v2f o;
					o.pos = UnityObjectToClipPos(v.vertex); //输入顶点坐标通过MVP矩阵变换到裁剪空间
					o.worldNormal= UnityObjectToWorldNormal(v.normal); //世界坐标法线转换
					o.worldPos = mul(unity_ObjectToWorld, v.vertex).xyz;  //顶点坐标世界空间
					o.uv = TRANSFORM_TEX(v.texcoord, _MainTex); //通过*tiling+offset将纹理图转换为uv
					return o;
				}

				fixed4 frag(v2f i):SV_Target
				{
					fixed3 worldNormal = normalize(i.worldNormal);
					fixed3 worldLightDir = normalize(UnityWorldSpaceLightDir(i.worldPos)); // 光照方向,这两项可以用来计算漫反射项
					fixed4 texColor = tex2D(_MainTex, i.uv); //对纹理进行采样

					//进行透明度测试,不符合要求的直接舍弃掉
					clip(texColor.a - _CutOff);
					fixed3 albedo = texColor.rgb*_Color.rgb;
					fixed3 diffuse = _LightColor0.rgb*albedo*max(dot(worldNormal, worldLightDir), 0);
					fixed3 ambient = UNITY_LIGHTMODEL_AMBIENT.xyz * albedo;

					return fixed4(diffuse+ambient, 1.0);
				}

			ENDCG
					
		}
			// Fallback "Transparent/Cutout/VertexLit"
	}
}

```

![image-20220223201738043](E:\各种总结指南\技术美术学习\技术美术相关\Unity Shader入门精要阅读\第八章 透明效果.assets\image-20220223201738043.png)



#### 8.4 透明度混合

**透明度混合：** 这种方法可以得到真正的半透明效果。它会使用当前片元的透明度作为混合因子，与已经存储在颜色缓冲中的颜色值进行混合，得到新的颜色。但是，透明度混合需要关闭深度写入，这使得我们要非常小心物体的渲染顺序。

![image-20220223202158850](E:\各种总结指南\技术美术学习\技术美术相关\Unity Shader入门精要阅读\第八章 透明效果.assets\image-20220223202158850.png)

在本节里，我们会使用第二种语义，即`Blend SrcFactor DstFactor`来进行混合。需要注意的是，**这个命令在设置混合因子的同时也开启了混合模式。**

> $DstColor_{new} = SrcAlpha × SrcColor + ( 1 − SrcAlpha ) × DstColor_{old}$



##### 本章目标2:

<img src="E:\各种总结指南\技术美术学习\技术美术相关\Unity Shader入门精要阅读\第八章 透明效果.assets\image-20220223202807747.png" alt="image-20220223202807747" style="zoom:80%;" />

1. 创建一个平面和一个立方体,最终实现的效果应该是可以通过立方体看到后面的平面.

```c#
Shader "MyShader/alpha/alpha1test2"
{
    Properties
    {
		_Color("Main Tint",Color)=(1,1,1,1)
        _MainTex ("Texture", 2D) = "white" {}
		_AlphaScale("AlphaScale",Range(0,1))=0.5
    }
    SubShader
    {
		Tags {"Queue" = "Transparent" "IgnoreProjector" = "True" "RenderType" = "Transparent"} //注意这里面的队列Queue要改为Transparent
		//见注解1部分
        Pass
        {
			Tags {"LightMode"="ForwardBase"}
			ZWrite Off
			Blend SrcAlpha OneMinusSrcAlpha  //见注解2部分
            CGPROGRAM
            #pragma vertex vert
            #pragma fragment frag

            #include "Lighting.cginc"

            struct a2v
            {
                float4 vertex : POSITION;
				float3 normal:NORMAL;
                float4 texcoord : TEXCOORD0;
            };

            struct v2f
            {
                float4 pos : SV_POSITION;
				float2 uv : TEXCOORD0;
				float3 worldNormal:TEXCOORD1;
				float3 worldPos:TEXCOORD2;
            };

            sampler2D _MainTex;
            float4 _MainTex_ST;  
			fixed4 _Color;
			fixed _AlphaScale;

			v2f vert(a2v v)
			{
				v2f o;
				o.pos = UnityObjectToClipPos(v.vertex); //输入顶点坐标通过MVP矩阵变换到裁剪空间
				o.worldNormal = UnityObjectToWorldNormal(v.normal); //世界坐标法线转换
				o.worldPos = mul(unity_ObjectToWorld, v.vertex).xyz;  //顶点坐标世界空间
				o.uv = TRANSFORM_TEX(v.texcoord, _MainTex); //通过*tiling+offset将纹理图转换为uv
				return o;
			}

			fixed4 frag(v2f i) :SV_Target
			{
				fixed3 worldNormal = normalize(i.worldNormal);
				fixed3 worldLightDir = normalize(UnityWorldSpaceLightDir(i.worldPos)); // 光照方向,这两项可以用来计算漫反射项
				fixed4 texColor = tex2D(_MainTex, i.uv); //对纹理进行采样
				
				fixed3 albedo = texColor.rgb*_Color.rgb;
				fixed3 diffuse = _LightColor0.rgb*albedo*max(dot(worldNormal, worldLightDir), 0);
				fixed3 ambient = UNITY_LIGHTMODEL_AMBIENT.xyz * albedo;

				return fixed4(diffuse + ambient, texColor.a*_AlphaScale); //见注解3
			}

            
            ENDCG
        }
    }
}

```

注解1:

在本章一开头，我们已经知道在Unity中**透明度混合使用的渲染队列是名为Transparent的队列**，因此我们需要把Queue标签设置为Transparent。`RenderType`标签可以让Unity把这个`Shader`归入到提前定义的组（这里就是Transparent组）中，用来指明该`Shader`是一个使用了透明度混合的`Shader`。`RenderType`标签通常被用于着色器替换功能。我们还把`IgnoreProjector`设置为True，这意味着这个`Shader`不会受到投影器（Projectors）的影响。通常，使用了透明度混合的`Shader`都应该在`SubShader`中设置这3个标签。



注解2:

Pass的标签仍和之前一样，即把`LightMode`设为`ForwardBase`，这是为了让Unity能够按前向渲染路径的方式为我们正确提供各个光照变量。除此之外，我们还把该Pass的深度写入（`ZWrite`）设置为关闭状态（Off），我们在之前已经讲过为什么要这样做了。这是非常重要的。然后，我们开启并设置了该Pass的混合模式。如在本节开头所讲的，我们将源颜色（该片元着色器产生的颜色）的混合因子设为`SrcAlpha`，把目标颜色（已经存在于颜色缓冲中的颜色）的混合因子设为`OneMinusSrcAlpha`，以得到合适的半透明效果。(**具体因子可以设置成什么值可以查看8.6节**)



注解3:

上述代码和8.3节中的几乎完全一样，只是移除了透明度测试的代码，并设置了该片元着色器返回值中的透明通道，它是纹理像素的透明通道和材质参数`_AlphaScale`的乘积。正如本节一开始所说的，只有使用Blend命令打开混合后，我们在这里设置透明通道才有意义，否则，这些透明度并不会对片元的透明效果有任何影响。

![image-20220223211612279](E:\各种总结指南\技术美术学习\技术美术相关\Unity Shader入门精要阅读\第八章 透明效果.assets\image-20220223211612279.png)



##### 关闭深度写入带来的问题

> 对于模型网格之间有互相交叉的结构时，往往会得到错误的半透明效果:

<img src="E:\各种总结指南\技术美术学习\技术美术相关\Unity Shader入门精要阅读\第八章 透明效果.assets\image-20220223211836612.png" alt="image-20220223211836612" style="zoom:80%;" />

这都是由于我们关闭了深度写入造成的，因为这样我们就无法对模型进行像素级别的深度排序。**在8.1节中我们提到了一种解决方法是分割网格，从而可以得到一个“质量优等”的网格。但是很多情况下这往往是不切实际的。**这时，我们可以想办法重新利用深度写入，让模型可以像半透明物体一样进行淡入淡出。这就是我们下面要讲的内容。



#### 8.5 开启深度写入的半透明效果

在8.4节最后，我们给出了一种由于关闭深度写入而造成的错误排序的情况。一种解决方法是**使用两个Pass** 来渲染模型：

- 第一个Pass开启深度写入，但不输出颜色，它的目的仅仅是为了把该模型的深度值写入深度缓冲中；
- 第二个Pass进行正常的透明度混合，**由于上一个Pass已经得到了逐像素的正确的深度信息，该Pass就可以按照像素级别的深度排序结果进行透明渲染。**但这种方法的缺点在于，多使用一个Pass会对性能造成一定的影响。

在本节最后，我们可以得到类似图8.11中的效果。可以看出，使用这种方法，我们仍然可以实现模型与它后面的背景混合的效果，但模型内部之间不会有任何真正的半透明效果。

<img src="E:\各种总结指南\技术美术学习\技术美术相关\Unity Shader入门精要阅读\第八章 透明效果.assets\image-20220223212402001.png" alt="image-20220223212402001" style="zoom:80%;" />

(对于每个像素来说,前面的会挡住后面的,因为我们开启了深度写入(但注意,我们不能改变缓冲区的颜色))

1.在原来的Shader的基础上,我们只需要加入一个Pass,放在原来Shader之前就可以了

```c#
Shader "Unity Shader Book/Chapter8-Alpha Blending ZWrite" {
    Properties {
        _Color ("Main Tint", Color) = (1,1,1,1)
        _MainTex ("Main Tex", 2D) = "white" {}
        _AlphaScale ("Alpha Scale", Range(0, 1)) = 1
    }
    SubShader {
        Tags {"Queue"="Transparent" "IgnoreProjector"="True" "RenderType"="Transparent"}

        // Extra pass that renders to depth buffer only
        Pass {
            ZWrite On
            ColorMask 0
        } //这个是新加入的Pass

        Pass {
            // 和8.4节同样的代码
        }
    } 
    Fallback "Diffuse"
}
```

**这个新添加的Pass的目的仅仅是为了把模型的深度信息写入深度缓冲中**，从而剔除模型中被自身遮挡的片元(所以后续对于这个模型来说,深度值小的会挡住深度值大的)。因此，Pass的第一行开启了深度写入。在第二行，我们使用了一个新的渲染命令——**ColorMask** 。在`ShaderLab`中，`ColorMask`用于设置颜色通道的写掩码（write mask）。它的语义如下：

`ColorMask RGB | A | 0 | 其他任何R、G、B、A的组合`

**当`ColorMask`设为0时，意味着该Pass不写入任何颜色通道，即不会输出任何颜色。这正是我们需要的——该Pass只需写入深度缓存即可。**

![image-20220223213944029](E:\各种总结指南\技术美术学习\技术美术相关\Unity Shader入门精要阅读\第八章 透明效果.assets\image-20220223213944029.png)



#### 8.6 ShaderLab的混合命令

首先来看一下混合是如何实现的。当片元着色器产生一个颜色的时候，可以选择与颜色缓存中的颜色进行混合。这样一来，混合就和两个操作数有关：**源颜色** **（source color）** 和**目标颜色（destination color）** 。

- 源颜色，我们用**S** 表示，指的是由片元着色器产生的颜色值；
- 目标颜色，我们用**D** 表示，指的是从颜色缓冲中读取到的颜色值。
- 对它们进行混合后得到的输出颜色，我们用**O** 表示，它会重新写入到颜色缓冲中。需要注意的是，当我们谈及混合中的源颜色、目标颜色和输出颜色时，它们都包含了RGBA四个通道的值，而并非仅仅是RGB通道。

> 想要使用混合，我们必须首先开启它。在Unity中，当我们使用Blend（Blend Off命令除外）命令时，除了设置混合状态外也开启了混合。但是，在其他图形API中我们是需要手动开启的。例如在OpenGL中，我们需要使用glEnable(GL_BLEND)来开启混合。但在Unity中，它已经在背后为我们做了这些工作。



##### 8.6.1 混合等式和参数

混合是一个逐片元的操作，而且它不是可编程的，但却是高度可配置的。也就是说，我们可以设置混合时使用的运算操作、混合因子等来影响混合。

现在，我们已知两个操作数：源颜色S和目标颜色D，想要得到输出颜色O就必须使用一个等式来计算。我们把这个等式称为**混合等式（blend equation）** 。

当进行混合时，我们需要使用两个混合等式：一个用于混合RGB通道，一个用于混合A通道。当设置混合状态时，我们实际上设置的就是混合等式中的操作和**因子** 。在默认情况下，混合等式使用的操作都是加操作（我们也可以使用其他操作），我们只需要再设置一下混合因子即可。由于需要两个等式（分别用于混合RGB通道和A通道），每个等式有两个因子（一个用于和源颜色相乘，一个用于和目标颜色相乘），因此一共需要4个因子。表8.3给出了ShaderLab中设置混合因子的命令。

![image-20220223214728131](E:\各种总结指南\技术美术学习\技术美术相关\Unity Shader入门精要阅读\第八章 透明效果.assets\image-20220223214728131.png)

![image-20220223215131117](E:\各种总结指南\技术美术学习\技术美术相关\Unity Shader入门精要阅读\第八章 透明效果.assets\image-20220223215131117.png)

那么，这些混合因子可以有哪些值呢？表8.4给出了ShaderLab支持的几种混合因子。

|      参 数       |                            描 述                             |
| :--------------: | :----------------------------------------------------------: |
|       One        |                           因子为1                            |
|       Zero       |                           因子为0                            |
|     SrcColor     | 因子为源颜色值。当用于混合RGB的混合等式时，使用SrcColor的RGB分量作为混合因子；当用于混合A的混合等式时，使用SrcColor的A分量作为混合因子 |
|     SrcAlpha     |               因子为源颜色的透明度值（A通道）                |
|     DstColor     | 因子为目标颜色值。当用于混合RGB通道的混合等式时，使用DstColor的RGB分量作为混合因子；当用于混合A通道的混合等式时，使用DstColor的A分量作为混合因子。 |
|     DstAlpha     |              因子为目标颜色的透明度值（A通道）               |
| OneMinusSrcColor | 因子为(1-源颜色)。当用于混合RGB的混合等式时，使用结果的RGB分量作为混合因子；当用于混合A的混合等式时，使用结果的A分量作为混合因子 |
| OneMinusSrcAlpha |                  因子为(1-源颜色的透明度值)                  |
| OneMinusDstColor | 因子为(1-目标颜色)。当用于混合RGB的混合等式时，使用结果的RGB分量作为混合因子；当用于混合A的混合等式时，使用结果的A分量作为混合因子 |
| OneMinusDstAlpha |                 因子为(1-目标颜色的透明度值)                 |

使用上面的指令进行设置时，RGB通道的混合因子和A通道的混合因子都是一样的，有时我们希望可以使用不同的参数混合A通道，这时就可以利用**Blend SrcFactor DstFactor, SrcFactorA DstFactorA** 指令。例如，如果我们想要在混合后，输出颜色的透明度值就是源颜色的透明度，可以使用下面的命令：

```c++
Blend SrcAlpha OneMinusSrcAlpha, One Zero
```



##### 8.6.2 混合操作

在上面涉及的混合等式中，当把源颜色和目标颜色与它们对应的混合因子相乘后，我们都是把它们的结果加起来作为输出颜色的。那么可不可以选择不使用加法，而使用减法呢？答案是肯定的，我们可以使用ShaderLab的**BlendOp BlendOperation**(这里的BlendOperation替换为下表当中的Add,Sub等) 命令，即混合操作命令。表8.5给出了ShaderLab中支持的混合操作。

![image-20220223215704629](E:\各种总结指南\技术美术学习\技术美术相关\Unity Shader入门精要阅读\第八章 透明效果.assets\image-20220223215704629.png)

混合操作命令通常是与混合因子命令一起工作的。但需要注意的是，当使用**Min** 或**Max** 混合操作时，混合因子实际上是不起任何作用的，它们仅会判断原始的源颜色和目的颜色之间的比较结果。



##### 8.6.3 常见的混合类型

通过混合操作和混合因子命令的组合，我们可以得到一些类似Photoshop混合模式中的混合效果：

```c++
// 正常（Normal），即透明度混合
Blend SrcAlpha OneMinusSrcAlpha

// 柔和相加（Soft Additive）
Blend OneMinusDstColor One

// 正片叠底（Multiply），即相乘
Blend DstColor Zero

// 两倍相乘（2x Multiply）
Blend DstColor SrcColor

// 变暗（Darken）
BlendOp Min
Blend One One

// 变亮（Lighten）
BlendOp Max
Blend One One

// 滤色（Screen）
Blend OneMinusDstColor One
// 等同于
Blend One OneMinusSrcColor

// 线性减淡（Linear Dodge）
Blend One One
```

![image-20220223220143603](E:\各种总结指南\技术美术学习\技术美术相关\Unity Shader入门精要阅读\第八章 透明效果.assets\image-20220223220143603.png)



#### 8.7 双面渲染的透明效果

在现实生活中，如果一个物体是透明的，意味着我们不仅可以透过它看到其他物体的样子，也可以看到它内部的结构。但在前面实现的透明效果中，无论是透明度测试还是透明度混合，我们都无法观察到正方体内部及其背面的形状，导致物体看起来就好像只有半个一样。这是因为，默认情况下渲染引擎剔除了物体背面（相对于摄像机的方向）的渲染图元，而只渲染了物体的正面。如果我们想要得到双面渲染的效果，可以使用**Cull** 指令来控制需要剔除哪个面的渲染图元。在Unity中，Cull指令的语法如下：

```
Cull Back | Front | Off
```

如果设置为Back，那么那些背对着摄像机的渲染图元就不会被渲染，这也是默认情况下的剔除状态；如果设置为Front，那么那些朝向摄像机的渲染图元就不会被渲染；如果设置为**Off** ，就会关闭剔除功能，那么所有的渲染图元都会被渲染，但由于这时需要渲染的图元数目会成倍增加，因此**除非是用于特殊效果，例如这里的双面渲染的透明效果，通常情况是不会关闭剔除功能的。**



##### 8.7.1 透明度测试的双面渲染

```c#
Pass {
    Tags { "LightMode"="ForwardBase" }

    // Turn off culling
    Cull Off
```

<img src="E:\各种总结指南\技术美术学习\技术美术相关\Unity Shader入门精要阅读\第八章 透明效果.assets\image-20220223221001425.png" alt="image-20220223221001425" style="zoom:80%;" />

##### 8.7.2　透明度混合的双面渲染

和透明度测试相比，**想要让透明度混合实现双面渲染会更复杂一些**，这是因为透明度混合需要关闭深度写入，而这是“一切混乱的开端”。我们知道，想要得到正确的透明效果，渲染顺序是非常重要的——**我们想要保证图元是从后往前渲染的。**

对于透明度测试来说，由于我们没有关闭深度写入，因此可以利用深度缓冲按逐像素的粒度进行深度排序，从而保证渲染的正确性。然而一旦关闭了深度写入，我们就需要小心地控制渲染顺序来得到正确的深度关系。如果我们仍然采用8.7.1节中的方法，直接关闭剔除功能，那么我们就无法保证同一个物体的正面和背面图元的渲染顺序，就有可能得到错误的半透明效果。

为此，**我们选择把双面渲染的工作分成两个Pass——第一个Pass只渲染背面，第二个Pass只渲染正面，由于Unity会顺序执行SubShader中的各个Pass，因此我们可以保证背面总是在正面被渲染之前渲染，从而可以保证正确的深度渲染关系。**

```c#
Shader "Unity Shaders Book/Chapter 8/Alpha Blend With Both Side" {
    Properties {
        _Color ("Main Tint", Color) = (1,1,1,1)
        _MainTex ("Main Tex", 2D) = "white" {}
        _AlphaScale ("Alpha Scale", Range(0, 1)) = 1
    }
    SubShader {
        Tags {"Queue"="Transparent" "IgnoreProjector"="True" "RenderType"="Transparent"}

        Pass {
            Tags { "LightMode"="ForwardBase" }

            // First pass renders only back faces 
            Cull Front

            // 和之前一样的代码
        }

        Pass {
            Tags { "LightMode"="ForwardBase" }

            // Second pass renders only front faces 
            Cull Back

            // 和之前一样的代码
        }
    } 
    Fallback "Transparent/VertexLit"
}
```

